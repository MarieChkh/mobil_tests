plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def aspectjWeaverVersion = "1.9.22",
    selenideVersion = "6.13.0",
    restAssuredVersion = "5.3.0",
    allureVersion = "2.21.0",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7",
    ownerVersion = "1.0.12"

allure {
    report {
        version = allureVersion
    }
    adapter {
        aspectjWeaver = true
        frameworks {
            junit5 {
                adapterVersion = allureVersion
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.aspectj:aspectjweaver:$aspectjWeaverVersion",
            'com.codeborne:selenide:' + selenideVersion,
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:8.3.0",
            'io.rest-assured:rest-assured:' + restAssuredVersion,
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.aeonbits.owner:owner:$ownerVersion"
    )
    testRuntimeOnly(
            "org.junit.platform:junit-platform-launcher")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.register('android_local', Test) {
    useJUnitPlatform {
        includeTags 'android_local'
    }
    systemProperty 'platform', 'android'
    systemProperty 'deviceHost', 'local'
}

tasks.register('android', Test) {
    useJUnitPlatform {
        includeTags 'android'
    }
    systemProperty 'platform', 'android'
    systemProperty 'deviceHost', 'browserstack'
}

tasks.register('ios', Test) {
    useJUnitPlatform {
        includeTags 'ios'
    }
    systemProperty 'platform', 'ios'
    systemProperty 'deviceHost', 'browserstack'
}